
%% The order here is important.

#inventory
(tutorial suggestion *)
(can perform * with [])
(suggest * with $)
    Try (bold) inventory (roman) \(or just 
    (bold) i (roman) \) to see what you are carrying.
(* is performed by [inventory])

#examine
(tutorial suggestion *)
(can perform * with $Object) 
    (current player $Player)
    %% Choose an object in the current room
    (current room $Room)
    *($Object is #in $Room)
    %% Exclude what the player carries
    ~($Object = $Player)
    ~($Object has ancestor $Player)
    *(player can see $Object)
(suggest * with $Item)
    Often, there will be interesting objects nearby; you can get more information
    about them by examining them;
    try (bold) examine (minimally name $Item) (roman)
    \(or just (bold) x (minimally name $Item) (roman) \).
(* is performed by [examine])
    
#look-self
(tutorial suggestion *)
(can perform * with [])
(suggest * with $)
    You are present as part of this world too. Try 
    (bold)look at me(roman)
    or (bold)l self(roman)
    to get an idea of who you are.

#take
(tutorial suggestion *)
(can perform * with $Item)
    *(item $Item)
    ~($Item is hidden)
    (player can see $Item)
    ~(out of reach $Item) 
    (current player $Player)
    ~($Item has ancestor $Player)
(suggest * with $Item)
    (if) ($Item is pristine)
    (then)
        The room description mentioned (a $Item).
    (else)
        There's (a $Item) nearby.
    (endif)
    You could (bold) take (minimally name $Item) (roman) to pick it up.
(* is performed by [take $])

#drop
(tutorial suggestion *)
(can perform * with $Item)
    (#inventory has been performed)
    (current player $Player)
    *($Item is $ $Player)
(suggest * with $Item)
    You are carrying (a $Item); you can leave it here with
    (bold) drop (minimally name $Item) (roman).
(* is performed by [drop $])

#wear
(tutorial suggestion *)
(can perform * with $Item)
    (#take has been performed)
    (current player $Player)
    *(wearable $Item)
    ~($Item has ancestor $Player)
    (player can see $Item)
    ~(out of reach $Item) 
(suggest * with $Item)
    (The $Item) is a piece of clothing; you can
    (bold) wear (minimally name $Item) (roman) to put it on.
(* is performed by [wear $])
(narrate after suggestion *)
    We try to be (italic)kind(roman); 
    if an object is nearby but not actually in your inventory, we'll automatically try to pick it up
    before wearing it. This applies to most verbs.

#leave
(tutorial suggestion *)
(can perform * with $Direction)
    (current room $Room)
    (from $Room go $Direction to $)
(suggest * with $Direction)
    (current room $Room)
    The world consists of distinct locations your can visit.
    You started in (name $Room), but could
    (bold)go (name $Direction)(roman)
    \( or just (name $Direction) \) to see 
    move to a different location.
    (par)
    Direction names include the eight cardinal directions \( 
    (bold)north (roman), 
    (bold)northeast(roman), 
    (bold)east(roman), etc.\)
    as well as (bold) up (roman),
    (bold)down(roman),
    (bold)in(roman), and
    (bold)out(roman)
    .
    (par)
    Finally, you can abbreviate most directions:
    (bold)n(roman) for (bold)north(roman),
    (bold)ne(roman) for (bold)northeast(roman), and so forth.
(* is performed by [go $])
(narrate after suggestion *)
    The world is not on a perfect grid; sometimes you go east, and return to
    where you started by going south \(not west\).  
    Sometimes a direction is a one-way trip, and you have to find a entirely different
    route back to where you started, if that is even possible.

#enter 
(tutorial suggestion *)
(can perform * with $Container)
    (current room $Room)
    *($Container is #in $Room)
    (actor container $Container)
(suggest * with $Container)
    Sometimes, you move around the world not by walking in any particular direction,
    but by going inside some of the things you find.  For example,
    you can (bold)enter (minimally name $Container)(roman)
    to see what's inside (the $Container).
(* is performed by [enter $])
(narrate after suggestion *)
    If you can (bold)enter(roman), you can probably (bold)leave(roman) as well.


