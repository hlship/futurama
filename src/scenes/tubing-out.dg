(global variable (tubing out start $))

#tubing-out
(scene *) 
(recurring *)
(start *)
    (#fry is #in #transport-tube)
    (current tick $Now)
    (now) (tubing out start $Now)
(complete *)
    ~(#fry is #in #transport-tube)
    (now) ~(tubing out start $)
(on every tick during *)
    (tubing out start $Start)
    (2 ticks elapsed since $Start)
    (par) 
    The raspy void announces, "I'm not gettting any younger bub. Just announce where ya wanna go."
(on every tick during *)
    (tubing out start $Start)
    (5 ticks elapsed since $Start)
    (par)
    The raspy voice complains, "Listen bub, announce a whadjamacallit, destination,
    or get out of the way!"
    (par)
    (#transport-tube is #in $Room)
    %% This disables the stdlib from making (the $Room) out as "this location":
    (now) ~(current room $)
    You notice a small but growing mob of impatient New New Yorkers behind you, never a good sign.
    You put your hands in the pockets of your jacket and shuffle back out to (the $Room).
    %% Dialog doesn't have a `try silently` so:
    (move player to #in $Room)
    (tick) (stop)

(understand $Words as [tube to $Where])
    (* in progress)
    (determine object $Where)
        *(tube location $Where)
    (from words)
        *(dict $Where)
    (matching all of $Words)

%% Prevent stdlib reachability check:
~(refuse [tube to $])

(prevent [tube to $Room])
    (current room $Room)
    The raspy voice corrects you. "Look around, willya?  You're already there."

(perform [tube to $Room])
    (current room $Start)
    (collect $Stop)
        *(tube location $Stop)
        ~($Stop = $Room)
        ~($Stop = $Start)
    (into $Stops)
    %% TODO: Randomize $Stops 
    (random from 2 to 3 into $Count)
    (take $Count from $Stops into $SomeStops)
    With a sound like a great tupperware container lid being opened, the tube sucks you in and
    hurtles you around New New York \(you catch fleeting images of (the $SomeStops)\) before
    you are spat out of the reversal chamber at (the $Room).
    You roll gracelessly back onto your feet and look around.
    (enter $Room)


(describe action [tube to $Room])
    announce (name $Room) as your destination

%% All tube locations ale also rooms
(room $Room) (tube location $Room)