(global variable (tubing out start $))

#tubing-out
(scene *) 
(recurring *)
(start *)
    (#fry is #in #transport-tube)
    (current tick $Now)
    (now) (tubing out start $Now)
(complete *)
    ~(#fry is #in #transport-tube)
    (now) ~(tubing out start $)
(on every tick during *)
    (tubing out start $Start)
    (2 ticks elapsed since $Start)
    (par) 
    The raspy void announces, "I'm not gettting any younger bub. Just announce where ya wanna go."
(on every tick during *)
    (tubing out start $Start)
    (5 ticks elapsed since $Start)
    (par)
    The raspy voice complains, "Listen bub, announce a whadjamacallit, destination,
    or get out of the way!"
    (par)
    (#transport-tube is #in $Room)
    %% This disables the stdlib from making (the $Room) out as "this location":
    (now) ~(current room $)
    You notice a small but growing mob of impatient New New Yorkers behind you, never a good sign.
    You put your hands in the pockets of your jacket and shuffle back out to (the $Room).
    %% Dialog doesn't have a `try silently` so:
    (move player to #in $Room)
    (tick) (stop)
(on every tick during *)
    (random chance of 2 in 3)
    (par)
    (current room $Room)
    (collect $Stop)
        *(tube location $Stop)
        ~($Stop = $Room)
    (into $Stops)
    (randomly select $Stop from $Stops)
    A New New Yorker elbows past you impatiently, announces "(no space)(name $Stop)," and is immediately sucked up
    into the tube system and away.

%% TODO:
%% check announcing a destination:
%%	 if the announced destination is nothing, say "The raspy voice responds: 'How about announcing someplace with a tube innit, will ya?'" instead.

(rewrite [say/announce/request | $Words] into $Words)
    (* in progress)

(understand $Words as [tube to $Where])
    (* in progress)
    (determine object $Where)
        *(tube location $Where)
    (from words)
        *(dict $Where)
    (matching all of $Words)

%% Prevent stdlib reachability check:
~(refuse [tube to $])

(prevent [tube to $Room])
    (current room $Room)
    The raspy voice sounds exasperated. "Look around, will ya, or announce someplace else instead, idjyat".

(perform [tube to $Room])
    (current room $Start)
    (collect $Stop)
        *(tube location $Stop)
        ~($Stop = $Room)
        ~($Stop = $Start)
    (into $Stops)
    (shuffle $Stops into $RandomStops)
    (random from 2 to 3 into $Count)
    %% Watch out: this fails if there are not enough to take!
    (take $Count from $RandomStops into $SomeStops)
    "I'd like to go to (the $Room)," you announce.
    (par)
    With a sound like a great tupperware container lid being opened, the tube sucks you in and
    hurtles you about New New York.  You catch fleeting images of (the $SomeStops) before
    you are spat out of the reversal chamber at (the $Room).
    You roll gracelessly back onto your feet and look around.
    (enter $Room)


(describe action [tube to $Room])
    announce (name $Room) as your destination

%% All tube locations ale also rooms
(room $Room) (tube location $Room)