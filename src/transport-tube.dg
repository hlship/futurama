%% The transport tube is how Fry moves around main locations in New New York.
%%
%% Certain rooms are marked as `(tube location $)` and are accessible from any other tube location.

#transport-tube
(name *) Transport Tube Entrance Bubble
(dict *) system chamber terminal station tubes
((tube location $) attracts *)
(actor container *)
(descr *)
    The transport tube systems connects just about everywhere in the city.
    A steady flow of people queue up to enter the entrance bubble and are sucked up by the
    outgoing tube; an equally steady flow of people arrive by the incoming tube
    and are ejected out of the reversing chamber.    
(appearance *)
    (current room $Room)
    %% Certain tube locations integrate a description of
    %% the transport tube system into their look rule, so no appearance is necessary.
    ~(omit tube appearance $Room)
    Nearby is a terminal of the city's transport tube system.
(narrate entering *)
    You shuffle on to the end of the fast moving line.
    The New New Yorkers ahead of you flit off to all parts of the city.
    Before you know it, you're at the head of the queue.
    You duck your head and walk inside the transparent bubble.
    A raspy, elderly voice, barely recognizable as a woman's, announces, 
    "What's your, whadjamacallit, destination?"
(narrate leaving *)
    You exit the bubble, squeezing past the line of New New Yorkers waiting to use the
    transport tube system.

#people
(name *) New New Yorkers
(dict *) people crowd mob
((tube location $) attracts *)
(plural *)
(descr *)
    They run the gamut from rich and stylish to poor and mutant.
(prevent $Action)
    ~($Action = [examine *])
    (* is one of $Action)
    You don't fuss with them, and they don't fuss with you.    